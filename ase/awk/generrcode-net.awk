#
# generrcode-net.awk
#
# aseawk -f generrcode-net.awk awk.h
#

BEGIN { 
	collect=0; 
	tab3="\t\t";
	tab4="\t\t\t";
}

/^[[:space:]]*enum[[:space:]]+ase_awk_errnum_t[[:space:]]*$/ { 
	collect=1; 
	print tab3 "// generated by generrcode-net.awk";
	print tab3 "enum class ERROR: int";
	print tab3 "{";
}

collect && /^[[:space:]]*};[[:space:]]*$/ { 
	print tab3 "};";
	print tab3 "// end of enum class ERROR";
	print "";
	collect=0; 
}

collect && /^[[:space:]]*ASE_AWK_E[[:alnum:]]+/ { 
	split ($1, flds, ",");
	name=flds[1];

	x = substr (name,10,length(name)-9);
	print tab4 x " = ASE::Awk::ERR_" x ",";
}

